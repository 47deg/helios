repositories {
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

apply plugin: "me.champeau.gradle.jmh"
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.47deg.hood'

dependencies {
    compile project(":helios-core")
    compile project(":helios-meta")
    compile project(":helios-parser")
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.0"
    compile 'com.beust:klaxon:3.0.11'
    compile 'com.tomatobean:jsonparser:1.0.10'
    compile 'com.squareup.moshi:moshi-kotlin:1.8.0'
    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
    compile "com.jsoniter:jsoniter:0.9.23"
    kapt "io.arrow-kt:arrow-annotations-processor:$arrowVersion"
    kapt project(':helios-meta-compiler')

}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

jmh {
    resultFormat = 'csv'
    resultsFile = file('build/reports/benchmarks.csv')
    timeOnIteration = '1s'
    if (project.hasProperty("jmhInclude"))
        include = [jmhInclude]
    if (project.hasProperty("jmhResultsFile"))
        resultsFile = file(jmhResultsFile)
}

task executeMasterBenchmark {
    doLast {
        jmh.resultsFile = file('build/reports/master_benchmark.csv')
        jmh.include = ["helios.benchmarks.HeliosBenchmark"]
    }
    finalizedBy 'jmh'
}

task executeHeliosBenchmark {
    doLast {
        jmh.resultsFile = file('build/reports/helios_benchmark.csv')
        jmh.include = ["helios.benchmarks.HeliosBenchmark"]
    }
    finalizedBy 'jmh'
}

task executeBenchmarks {
    doLast {
        jmh.resultsFile = file('build/reports/benchmarks.csv')
        jmh.include = ["helios.benchmarks.HeliosBenchmark", "helios.benchmarks.Decoding", "helios.benchmarks.DecodingFromRaw", "helios.benchmarks.Parsing"]
    }
    finalizedBy 'jmh'
}

compareBenchmark {
    previousBenchmarkPath = file('benchmarks/master_benchmark.csv')
    currentBenchmarkPath = [file('build/reports/helios_benchmark.csv')]
    threshold = 600
}

compareBenchmarkCI {
    previousBenchmarkPath = file('benchmarks/master_benchmark.csv')
    currentBenchmarkPath = [file('build/reports/helios_benchmark.csv')]
    threshold = 600
    token = System.getenv('GITHUB_ACCESS_TOKEN')
}

uploadBenchmark {
    benchmarkFile = file('build/reports/master_benchmark.csv')
    benchmarkDestinationFromProjectRoot = "benchmarks/master_benchmark.csv"
    token = System.getenv('GITHUB_ACCESS_TOKEN')
}
